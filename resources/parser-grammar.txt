program         -> declaration* EOF ;
declaration     -> varDeclaration | statement ;
varDeclaration  -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement       -> exprStmt | ifStmt | whileStmt | forStmt | printStmt | block ;
exprStmt        -> expression ";" ;
ifStmt          -> "if" "(" expression ")" statement ( "else" statement )? ;
whileStmt       -> "while" "(" expression ")" statement ;
forStmt         -> "for" "(" ( varDeclaration | exprStmt ) ";" expression ";" expression ")" statement ;
printStmt       -> "print" expression ";" ;
block           -> "{" declaration* "}" ;
expression      -> assignment ;
assignment      -> IDENTIFIER "=" assignment | logicOr ;
logicOr         -> logicAnd ( "or" logicAnd )* ;
logicAnd        -> equality ( "and" equality )* ;
equality        -> comparison ( ( "==" | "!=" ) comparison )* ;
comparison      -> term ( ( "<" | "<=" | ">=" | ">" ) term )* ;
term            -> factor ( ( "+" | "-" ) factor )* ;
factor          -> unary ( ( "*" | "/" ) unary )* ;
unary           -> ( "!" | "-" ) unary | primary ;
primary         -> NUMBER | STRING | "true" | "false" | "nil" | IDENTIFIER | "(" expression ")" ;